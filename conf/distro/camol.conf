#@--------------------------------------------------------------------
#@TYPE: Distribution
#@NAME: Cambrionix Linux <http://www.cambrionix.com>
#@DESCRIPTION: The Linux Distribution for Kernel 3.14 based devices
#@MAINTAINER: Cambrionix IT <it@cambrionix.com>
#@--------------------------------------------------------------------

DISTRO_VERSION = "v${@time.strftime('%Y.%m',time.gmtime())}"

require conf/distro/include/sane-srcdates.inc
require conf/distro/include/sane-srcrevs.inc
require conf/distro/include/camol-preferred-versions.inc

#Images built can have to modes:
# 'debug': empty rootpassword, strace included
# 'release' no root password, no strace and gdb by default
DISTRO_TYPE ?= "debug"
#DISTRO_TYPE = "release"

# Set the toolchain type (internal, external) and brand (generic, csl etc.)
TOOLCHAIN_TYPE ?= "internal"
TOOLCHAIN_BRAND ?= ""

# Ship just basic locale by default. Locales are big (~1Mb uncompr.), so
# shipping some adhoc subset will be still useless and size burden for
# users of all other languages/countries. Instead, worth to make it easy
# to install additional languages: installer/wizard + metapackages which
# will RRECOMMEND as much as possible content for a given language
# (locales, UI transalations, help, etc. - useless for pros, but really 
# helpful for common users).
# Also, it appears that no locales fit in 16Mb for now. "C" locale rules!
ROOT_FLASH_SIZE ??= "32"
IMAGE_LINGUAS ?= '${@base_less_or_equal("ROOT_FLASH_SIZE", "16", "", "en-us", d)}'

# set feed path variables
FEED_BASEPATH = "feeds/next/${CAMOL_PKG_FORMAT}/${TCLIBC}/"


#Everybody else can just use this:
CAMOL_GCC_VERSION                             ?= "4.9%"
CAMOL_BINUTILS_VERSION                        ?= "2.27%"
CAMOL_GDB_VERSION                             ?= "7.9%"

#use Linaro tools for ARM
CAMOL_GCC_VERSION_arm                         ?= "linaro-4.9%"
CAMOL_BINUTILS_VERSION_arm                    ?= "linaro-%"
CAMOL_GDB_VERSION_arm                         ?= "linaro-%"
CAMOL_GCC_VERSION_aarch64                     ?= "linaro-4.9%"
CAMOL_BINUTILS_VERSION_aarch64                ?= "linaro-%"
CAMOL_GDB_VERSION_aarch64                     ?= "linaro-%"
PREFERRED_VERSION_cross-localedef-native         ?= "2.2%"

CAMOL_QEMU_VERSION                            ?= "2%"

GCCVERSION                                       = "${CAMOL_GCC_VERSION}"
BINUVERSION                                      = "${CAMOL_BINUTILS_VERSION}"
GDBVERSION                                       = "${CAMOL_GDB_VERSION}"
PREFERRED_VERSION_qemu                           = "${CAMOL_QEMU_VERSION}"
PREFERRED_VERSION_qemu-native                    = "${CAMOL_QEMU_VERSION}"
PREFERRED_VERSION_nativesdk-qemu                 = "${CAMOL_QEMU_VERSION}"
PREFERRED_VERSION_util-linux			 = "2.30"

PREFERRED_PROVIDER_dbus-glib                     = "dbus-glib"
PREFERRED_PROVIDER-gconf-dbus                    = "gconf"
PREFERRED_PROVIDER_hotplug                       = "systemd"
PREFERRED_PROVIDER_opkg                          ?= "opkg"
PREFERRED_PROVIDER_opkg-native                   ?= "opkg-native"

# Prefer gummiboot over grub
EFI_PROVIDER                                     = "gummiboot"

# blacklist policy

PNBLACKLIST[pn-fso-apm]       = "regular apmd is good enough"
PNBLACKLIST[gconf-dbus]        = "gconf-dbus has been merged back into main GConf"
PNBLACKLIST[gconf-dbus-native] = "gconf-dbus has been merged back into main GConf"
PNBLACKLIST[ffmpeg] = "libav is preferred over ffmpeg"

require conf/distro/include/camol.inc

# Toolchain virtuals:
require conf/distro/include/toolchain-${TOOLCHAIN_TYPE}.inc
# Processor specific tunes like hard float ABI
include conf/distro/include/${TARGET_ARCH}-defaults.inc
# Set DISTRO_FEED_CONFIGS to our config var, assigned in the above .inc
DISTRO_FEED_CONFIGS = "${CAMOL_FEED_CONFIGS}"

# If we're using an .ipk based rootfs, we want to have opkg installed so postinst script can run
# We also take this opportunity to inject camol-version and the feed configs into the rootfs
IPKG_VARIANT = "opkg camol-version ${CAMOL_FEED_CONFIGS}"

# Select xserver-xorg as default, since kdrive has been EOL'ed
XSERVER ?= "xserver-xorg xf86-input-evdev xf86-input-keyboard xf86-input-mouse xf86-video-fbdev"

# do some packagegroup-base stuff here

# Prefer bluez4, it's needed for things like connman. Bluez4 is also largely backward compatible with
# bluez 3.x
DISTRO_BLUETOOTH_MANAGER = "\
	blueprobe \
	bluez5 \
   "

KERNEL_MODULE_PACKAGE_SUFFIX = ""

# We want to ship extra debug utils in the rootfs when doing a debug build
DEBUG_APPS ?= ""
DEBUG_APPS += '${@base_conditional("DISTRO_TYPE", "release", "", "strace procps",d)}'

# This hooks into packagegroup-base, so it won't do anything if your images doesn't include packagegroup-base.
# camol-version: ship this to have an identifiable rootfs so user can report bugs against a specific version
# CAMOL_FEED_CONFIGS: configfiles for the online feeds
# util-linux-mount util-linux-umount: busybox mount is broken
# camol-libc-fixup-hack: fixes an obscure bug with libc.so symlink
DISTRO_EXTRA_RDEPENDS += "\
    camol-version \
    ${CAMOL_FEED_CONFIGS} \
    util-linux-mount util-linux-umount \
    camol-libc-fixup-hack \
    "

# This also hooks into packagegroup-base, but isn't mandatory.
# If you don't want parts of this in your packagegroup-base using images you can put this in the image recipe:
# BAD_RECOMMENDATIONS = "avahi-daemon avahi-autoipd"
# Note that BAD_RECOMMENDATIONS is a feature of rootfs_ipk.bbclass, not camol
# kernel modules: ship fs modules so you can mount stuff and af-packet so networking works
# openssh-sftp-server: provides sftp which combined with avahi makes it real easy to use things like sshfs
# DEBUG_APPS: ship strace and procpc to make simple debugging a lot easier
DISTRO_EXTRA_RRECOMMENDS += " \
    kernel-module-vfat \
    kernel-module-ext2 \
    kernel-module-ext3 \
    kernel-module-af-packet \
    bonjour \
    bonjour-camol \
    openssh-sftp-server \
    ${DEBUG_APPS} \
"
